
String NATIVE_SRC_DIR = "src/main/jni"
String NATIVE_BUILD_DIR = "src/main/jni/cmake-build-release"
String NATIVE_OUTPUT_DIR = "src/main/resources/native/lib"

buildscript {

	ext {
		set('kotlinVersion', '1.3.61')
	}

	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
	}
}

apply plugin: 'kotlin'

group = 'com.kotlinbug'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

test {
	useJUnitPlatform()
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
	implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
	compile 'com.shankyank.jni:jni-loader:0.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

task compileNative() {
	inputs.files(fileTree(NATIVE_SRC_DIR))
	outputs.dirs(NATIVE_OUTPUT_DIR)
	doLast {
		exec {
			commandLine "sh", "./build-native.sh"
		}
		copyNatives("darwin-x86_64", "**/*.dylib")
		copyNatives("linux-x86_64", "**/*.so")
		copyNatives("linux-x86_64", "**/*.so.*")
		copyNatives("windows-x86_64", "**/*.dll")
	}
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
	dependsOn compileNative
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

clean {
	doLast {
		followSymlinks = true
		delete file(NATIVE_BUILD_DIR)
		delete file(NATIVE_OUTPUT_DIR)
	}
}

ext.copyNatives = { String os, String type ->
	copy {
		from NATIVE_BUILD_DIR
		include type
		into NATIVE_OUTPUT_DIR + "/" + os
		eachFile {
			path = name
		}
		includeEmptyDirs = false
	}
	fileTree("${projectDir}/${NATIVE_OUTPUT_DIR}/${os}/").files.each { it ->
		File file = it.absoluteFile
		exec {
			commandLine "zip", "-1", "-o", "-u", "-j", "${projectDir}/${NATIVE_OUTPUT_DIR}/libs-${os}.zip", file
		}
	}
	file(NATIVE_OUTPUT_DIR + "/" + os).deleteDir()
}
